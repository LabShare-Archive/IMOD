#!/bin/csh -f
# Script to add Qt libraries and links to a distribution file
#
#  $Author$
#
#  $Date$
#
#  $Revision$
#
#  $Log$
#  Revision 3.9  2003/08/25 18:47:13  mast
#  Have it make the install directory if needed, and delete existing links
#
#  Revision 3.8  2003/08/13 20:01:41  mast
#  Have it use LIBEXT from configure if available, add test for libqt.la
#
#  Revision 3.7  2003/05/08 23:29:09  mast
#  change for windows
#
#  Revision 3.6  2003/05/08 21:42:35  mast
#  Get libqt name better
#
#  Revision 3.5  2003/05/08 20:58:43  mast
#  Initial creation (deleted messages while thrashing
#
#

#
# Get the subdirectory to copy the libraries to, exit if not defined
#
set qtlibdir = `sed -n '/QTINSTLIBDIR/s/^.*= *//p' configure`

if ($#qtlibdir == 0) exit 0
if ($qtlibdir == "") exit 0

#
# Get the library extension, but don't worrry if it's not defined
#
set libext = `sed -n '/LIBEXT/s/^.*= *//p' configure`

#
# get the install directory, and grab the name of the Qt lib from the
# ever-useful sendevent Makefile
#
set instdir = `sed -n '/INSTDIR/s/^.*= *//p' configure`

set qtlib = `sed -n '/^LIBS.*=/s/^.*-lqt/libqt/p' sendevent/Makefile`

# If the library goes to bin, it is Windows and need to copy a dll
if ($qtlibdir == "bin") then
    set qtlib = $qtlib[2]
    \cp $QTDIR/lib/${qtlib}*.dll $instdir/$qtlibdir
    exit 0
endif

set qtlib = $qtlib[1]

#
# Count up non-links and save name of real file
#
@ numreal = 0
foreach i ($QTDIR/lib/$qtlib.*$libext)
    if ((! -l $i) && $i:e != "prl" && $i:e != "la") then
        @ numreal++
        set realname = $i
    endif
end

if ($numreal == 0) then
        echo "installqtlib: Qt Library not found at $QTDIR/lib/$qtlib.*"
        exit 1
endif

if ($numreal > 1) then
    echo "installqtlib: $numreal real Qt libraries match $QTDIR/lib/$qtlib.*"
    echo Cannot make links properly
    exit 1
endif

#
# Make directory, copy the real file, make the links
#
if (! -e $instdir/$qtlibdir) mkdir -p $instdir/$qtlibdir

cd $instdir/$qtlibdir
\cp $realname .
foreach i ($QTDIR/lib/${qtlib}*$libext)
    if (-l $i) then
        \rm -f $i:t
        ln -s $realname:t $i:t
    endif
end
