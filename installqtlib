#!/bin/csh -f
# Script to add Qt libraries and links to a distribution file, and copy other
# files as needed for particular system
#
#  $Id$
#  Log at end of file
#

# get the install directory
#
set instdir = `sed -n '/INSTDIR/s/^.*= *//p' configure`

#
# Get list of install files and copy them to install directory
#
set instlist = `sed -n '/INSTALL_FILES/s/^.*= *//p' configure`
if ("$instlist" != "") then
    foreach i ($instlist)
        \cp dist/$i $instdir
    end
endif

#
# Get list of install files that need renaming and copy them to install directory
#
set instlist = `sed -n '/INSTALL_RENAME/s/^.*= *//p' configure`
while ($#instlist > 1)
    \cp dist/$instlist[1] $instdir/$instlist[2]
    shift instlist
    shift instlist
end

# Get list that need renaming to be called through wrappers for Vista UAC
#
set wraplist = `sed -n '/UAC_WRAPPERS/s/^.*= *//p' configure`
while ($#wraplist > 1)
    set fromname = $instdir/bin/$wraplist[1]
    set toname = $instdir/bin/$wraplist[2]
    \mv $fromname.exe $toname.exe
    \mv $toname $fromname
    if (-e $toname.cmd) \mv $toname.cmd $fromname.cmd
    shift wraplist
    shift wraplist
end

# Get the subdirectory to copy the libraries to
#
set qtlibdir = `sed -n '/QTINSTLIBDIR/s/^.*= *//p' configure`

if ($#qtlibdir == 0) set qtlibdir = ""

# Copy Qt assistant to this location, or to bin if not defined
#
set assistdir = $qtlibdir
if ($assistdir == "") set assistdir = bin
if (! -e $instdir/$assistdir) mkdir -p $instdir/$assistdir

# Qt assistant could be an exe or a .app directory
#
if (`\find $QTDIR/bin -name 'assistant*' -print` != "") then
    \cp -r $QTDIR/bin/assistant* $instdir/$assistdir
else
    echo "Qt Assistant not found in $QTDIR/bin"
    exit 1
endif

# Copy Qt plugins to imodplug since it is present everywhere
#
@ plugnum = 1
while ($plugnum < 100)
    set plugdir = `sed -n "/QTPLUG_SRCDIR$plugnum/s/^.*= *//p" configure`
    if ("$plugdir" == "") break
    set pluglist = `sed -n "/QTPLUGS_TOCOPY$plugnum/s/^.*= *//p" configure`
    mkdir -p $instdir/lib/imodplug/$plugdir
    foreach i ($pluglist)
        \cp $QTDIR/plugins/$plugdir/$i $instdir/lib/imodplug/$plugdir
    end
    @ plugnum++
end

# Get a DLL destination dir
#
set dlldestdir = `sed -n '/DLLDESTDIR/s/^.*= *//p' configure`
if ("$dlldestdir" == "") set dlldestdir = $qtlibdir

# Done if no destination location defined
#
if ($dlldestdir == "") exit 0

# Get the dynamic library extension, which can be recursively defined
#
set libext = `sed -n '/^LIBEXT.*=/s/^.*= *//p' configure`
set dllext = `sed -n '/^DLLEXT.*=/s/^.*= *//p' configure | sed "/.*LIBEXT.*/s//$libext/"`
set dllrenlist = `sed -n "/DLLSTORENAME/s/^.*= *//p" configure`

#
# Copy DLL's from various places to the install directory
#
@ dllnum = 1
while ($dllnum < 100)
    set dlldir = `sed -n "/DLLSOURCEDIR$dllnum/s/^.*= *//p" configure`

    set dlllist = `sed -n "/DLLSTOCOPY$dllnum/s/^.*= *//p" configure`
    if ("$dlllist" != "") then
        foreach i ($dlllist)
            \cp -R "$dlldir"/$i.*$dllext* $instdir/$dlldestdir
        end
    else
        break
    endif
    @ dllnum++
end

if ("$dllrenlist" != "") then
    pushd $instdir/$dlldestdir
    foreach i ($dllrenlist)
        foreach j (${i}*)
            \mv $j imod-$j
        end
    end
    popd
endif

# Now done if no Qt library location defined
#
if ($qtlibdir == "") exit 0

# Make directory, copy the real file, make the links
#
if (! -e $instdir/$qtlibdir) mkdir -p $instdir/$qtlibdir

#
# grab the name of the Qt lib from the ever-useful sendevent Makefile
#
set qtlib = `sed -n '/^LIBS.*=/s/^.*-lqt/libqt/p' sendevent/Makefile`

# If the library goes to bin, it is Windows and need to copy a dll
# First get rid of any lib's that installed into the bin with the dll's
#
if ($qtlibdir == "bin") then
    \find $instdir/$qtlibdir -name '*.lib' -print -exec \rm '{}' \;
    if (`\find $QTDIR/lib -name 'qt-mt*.dll' -print` != "") then
        \cp $QTDIR/lib/qt-mt*.dll $instdir/$qtlibdir
    else if (`\find $QTDIR/lib -name 'qt-*.dll' -print` != "") then
        \cp $QTDIR/lib/qt-*.dll $instdir/$qtlibdir
    else if (`\find $QTDIR/lib -name 'qt*.dll' -print` != "") then
        \cp $QTDIR/lib/qt*.dll $instdir/$qtlibdir
    else
        echo "INSTALLQTLIB CANNOT FIND LIBRARY IN $QTDIR/lib"
        exit 1
    endif

    # 6/25/04: discovered these were not coming through right, and it matters
    #
    chmod a+rx $instdir/$qtlibdir/*.dll $instdir/$qtlibdir/assistant*
    exit 0
endif

set qtlib = $qtlib[1]

#
# For qt lib and possible assistant lib, count up non-links and save name of 
# real file
#
@ whichlib = 0
while ($whichlib < 2)
    @ numreal = 0
    foreach i ($QTDIR/lib/${qtlib}.*${dllext}*)
        if ((! -l $i) && $i:e != "prl" && $i:e != "la") then
            @ numreal++
            set realname = $i
        endif
    end

    if ($numreal == 0) then
        if ($whichlib > 0) break
        echo "installqtlib: Qt Library not found at $QTDIR/lib/$qtlib.*"
        exit 1
    endif

    if ($numreal > 1) then
        echo "installqtlib: $numreal real Qt libraries match $QTDIR/lib/$qtlib.*"
        echo Cannot make links properly
        exit 1
    endif

    cd $instdir/$qtlibdir
    \cp $realname .
    foreach i ($QTDIR/lib/${qtlib}*${dllext}*)
        if (-l $i) then
            \rm -f $i:t
            ln -s $realname:t $i:t
        endif
    end

    #
    # Copy libqassistantclient the same way if it is a shared lib
    #
    set qtlib = libqassistantclient
    if (! -e $QTDIR/lib/${qtlib}.${dllext}) break;
    @ whichlib++
end
exit 0

#
#  $Log$
#  Revision 3.29  2007/12/15 00:00:11  mast
#  Added wrappers for Vista patch executables
#
#  Revision 3.28  2006/10/19 23:42:56  mast
#  Copy libraries with wild card after as well as before the extension
#
#  Revision 3.27  2006/07/17 18:46:30  mast
#  Added Qt plugin copying
#
#  Revision 3.26  2006/06/21 21:26:38  mast
#  Generalized the dll copy logic to work for Mac, and to take names without
#  extensions, and to take destination directory
#
#  Revision 3.25  2005/02/24 04:03:32  mast
#  Needed to make directory for assistant if not there yet
#
#  Revision 3.24  2005/02/23 03:44:12  mast
#  Needed to put assistant in bin for Linux without Qt included
#
#  Revision 3.23  2004/11/22 06:06:39  mast
#  Move creation of qtlib up
#
#  Revision 3.22  2004/11/22 05:32:19  mast
#  Check for existence of qassistant lib before going through loop again
#
#  Revision 3.21  2004/11/22 00:19:51  mast
#  Added Qt assistant and library if needed
#
#  Revision 3.20  2004/06/26 02:46:51  mast
#  Needed to make all DLLs read/execute by all
#
#  Revision 3.19  2004/04/03 20:01:00  mast
#  Added ability to copy install files and rename them
#
#  Revision 3.18  2004/01/27 05:53:54  mast
#  Needed to define LIBEXT line better
#
#  Revision 3.17  2004/01/27 05:16:26  mast
#  Start out by copying needed files from dist
#
#  Revision 3.16  2003/12/30 17:24:58  mast
#  Delete stray .libs in windows install
#
#  Revision 3.15  2003/12/02 02:07:08  mast
#  Change the way Qt lib is identified in Windows, give up on Makefiles
#
#  Revision 3.14  2003/11/26 21:48:08  mast
#  Allow large number of dll lists to copy
#
#  Revision 3.13  2003/10/25 16:25:59  mast
#  fix for Cygwin/Intel and add ability to copy other libraries
#
#  Revision 3.12  2003/10/25 02:52:27  mast
#  find library in cygwin/Intel makefile
#
#  Revision 3.11  2003/10/03 05:04:45  mast
#  Discovered extension can go before (SGI) or after (Mac) version numbers
#
#  Revision 3.10  2003/09/25 00:04:08  mast
#  Improve test for no entry in configure
#
#  Revision 3.9  2003/08/25 18:47:13  mast
#  Have it make the install directory if needed, and delete existing links
#
#  Revision 3.8  2003/08/13 20:01:41  mast
#  Have it use LIBEXT from configure if available, add test for libqt.la
#
#  Revision 3.7  2003/05/08 23:29:09  mast
#  change for windows
#
#  Revision 3.6  2003/05/08 21:42:35  mast
#  Get libqt name better
#
#  Revision 3.5  2003/05/08 20:58:43  mast
#  Initial creation (deleted messages while thrashing
#
#
