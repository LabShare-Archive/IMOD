#!/bin/csh -f
# Script to add Qt libraries and links to a distribution file, and copy other
# files as needed for particular system
#
#  $Id$
#  Log at end of file
#

# get the install directory
#
set instdir = `sed -n '/INSTDIR/s/^.*= *//p' configure`

#
# Get list of install files and copy them to install directory
#
set instlist = `sed -n '/INSTALL_FILES/s/^.*= *//p' configure`
if ("$instlist" != "") then
    foreach i ($instlist)
        \cp dist/$i $instdir
    end
endif

#
# Get list of install files that need renaming and copy them to install directory
#
set instlist = `sed -n '/INSTALL_RENAME/s/^.*= *//p' configure`
while ($#instlist > 1)
    \cp dist/$instlist[1] $instdir/$instlist[2]
    shift instlist
    shift instlist
end

# Get list that need renaming to be called through wrappers for Vista UAC
# Scripts must already have the final name in order for .cmd wrapper to work
#
set wraplist = `sed -n '/UAC_WRAPPERS/s/^.*= *//p' configure`
while ($#wraplist > 1)
    set fromname = $instdir/bin/$wraplist[1]
    set toname = $instdir/bin/$wraplist[2]
    \mv $fromname.exe $toname.exe
    shift wraplist
    shift wraplist
end

# Get the subdirectory to copy the libraries to
#
set qtlibdir = `sed -n '/QTINSTLIBDIR/s/^.*= *//p' configure`

if ($#qtlibdir == 0) set qtlibdir = ""

# Copy Qt assistant to this location, or to bin if not defined
#
set assistdir = $qtlibdir
if ($assistdir == "") set assistdir = bin
if (! -e $instdir/$assistdir) mkdir -p $instdir/$assistdir

# Qt assistant could be an exe or a .app directory
#
set assistname = `\find $QTDIR/bin -maxdepth 1 -name '*ssistant_ad*[^g]' -print`
if ("$assistname" != "") then
    \cp -r $assistname $instdir/$assistdir
else
    echo "Qt Assistant not found in $QTDIR/bin"
    exit 1
endif

# Make a wrapper script for the Qt assistant in qtlib but make it lower case
# 
if (-e $instdir/qtlib) then
    set app = `\find $instdir/qtlib -name '*ssistant*.app' -print`
    if ("$app" != "") then
        set prog = $app:t:r
        if (! -e $instdir/qtlib/$prog) then
            sed -e "/imodsendevent/s//$prog/g" -e "/IMOD_DIR/s/bin/qtlib/" \
                $instdir/bin/imodsendevent > $instdir/qtlib/assistant_adp
            chmod a+rx $instdir/qtlib/assistant_adp
        endif
    endif
endif

# Copy Qt plugins to imodplug since it is present everywhere
#
@ plugnum = 1
while ($plugnum < 100)
    set plugdir = `sed -n "/QTPLUG_SRCDIR$plugnum/s/^.*= *//p" configure`
    if ("$plugdir" == "") break
    set pluglist = `sed -n "/QTPLUGS_TOCOPY$plugnum/s/^.*= *//p" configure`
    mkdir -p $instdir/lib/imodplug/$plugdir
    foreach i ($pluglist)
        if (-e $QTDIR/plugins/$plugdir/$i) then
            \cp $QTDIR/plugins/$plugdir/$i $instdir/lib/imodplug/$plugdir
            chmod -R 755 $instdir/lib/imodplug/$plugdir
        endif
    end
    @ plugnum++
end

# Get a DLL destination dir
#
set dlldestdir = `sed -n '/DLLDESTDIR/s/^.*= *//p' configure`
set dlldestorig = $dlldestdir
if ("$dlldestdir" == "") set dlldestdir = $qtlibdir

# Done if no destination location defined
#
if ($dlldestdir == "") exit 0

# Get the dynamic library extension, which can be recursively defined
#
set libext = `sed -n '/^LIBEXT.*=/s/^.*= *//p' configure`
set dllext = `sed -n '/^DLLEXT.*=/s/^.*= *//p' configure | sed "/.*LIBEXT.*/s//$libext/"`
set dllrenlist = `sed -n "/DLLSTORENAME/s/^.*= *//p" configure`

#
# Copy DLL's from various places to the install directory
#
@ dllnum = 1
while ($dllnum < 100)
    set dlldir = `sed -n "/DLLSOURCEDIR$dllnum/s/^.*= *//p" configure`

    set dlllist = `sed -n "/DLLSTOCOPY$dllnum/s/^.*= *//p" configure`
    if ("$dlllist" != "") then
        foreach i ($dlllist)
            \cp -R "$dlldir"/$i.*$dllext* $instdir/$dlldestdir
        end
    else
        break
    endif
    @ dllnum++
end

if ("$dllrenlist" != "") then
    pushd $instdir/$dlldestdir
    foreach i ($dllrenlist)
        foreach j (${i}*)
            \mv $j imod-$j
        end
    end
    popd
endif

# If the destination dir was qtlib, then all programs need wrapper scripts
# This should work for Mac too - but (4/7/09) the existing wrappers for Qt 4.5
# need to have both DYLD_LIBRARY_PATH and DYLD_FRAMEWORK_PATH set
#
if ("$dlldestorig" == "qtlib") then
    pushd $instdir/bin
    set envar = LD_LIBRARY_PATH
    if ("$dllext" == "dylib") set envar = DYLD_LIBRARY_PATH
    mkdir -p realbin
    foreach i (*)
        file $i | grep '\-bit.*executable' >& /dev/null
        if (! $status) then
            mv $i realbin
            cat << EOF > $i
#!/bin/sh
export ${envar}="\${IMOD_DIR}/qtlib:\$$envar"
"\$IMOD_DIR/bin/realbin/$i" "\$@"
EOF
        chmod a+rx $i
        endif
    end

    # Fix 3dmodv link to have its own script with the -view option
    #
    if (-l 3dmodv) then
        rm 3dmodv
        sed '/mod\" \"/s/ / -view /' < 3dmod >! 3dmodv
        chmod a+rx 3dmodv
    endif
    popd
endif


# Now done if no Qt library location defined
#
if ($qtlibdir == "") exit 0

# Make directory, copy the real file, make the links
#
if (! -e $instdir/$qtlibdir) mkdir -p $instdir/$qtlibdir

#
# Get names of libs to copy
#
set qtlibs = `sed -n '/QTLIBSTOCOPY/s/^.*= *//p' configure`

# If the library goes to bin, it is Windows and need to copy a dll
# First get rid of any lib's that installed into the bin with the dll's
#
if ($qtlibdir == "bin") then
    \find $instdir/$qtlibdir -name '*.lib' -print -exec \rm '{}' \;
    foreach i ($qtlibs)  
        \cp  $QTDIR/lib/${i}4.dll $instdir/$qtlibdir
        if ($status) then
            echo "INSTALLQTLIB CANNOT FIND LIBRARY ${i}4.dll IN $QTDIR/lib"
            exit 1
        endif
    end

    # 6/25/04: discovered these were not coming through right, and it matters
    # 3/13/09: also need u+w, so just do 755
    chmod 755 $instdir/$qtlibdir/*.dll $instdir/$qtlibdir/assistant*


    # Now copy redistributable directory and install manifests
    #
    if ($?MSVCREDIST) then
        set manifest = `sed -n '/MANIFEST/s/^.*= *//p' configure`
        set redist = "$MSVCREDIST:t"
        \cp -r "$MSVCREDIST" $instdir/bin
        chmod -R 755 $instdir/bin/$redist
        foreach i ($instdir/bin/*.exe)
            if ("$i:t" != $assistname) then
                set icyg = `cygpath -w $i`
                set errout = `mt "-outputresource:$icyg;#1" -manifest $manifest`
                set errstat = $status
                if ($errstat) then
                    \echo "Error code $errstat running: mt -outputresource:$icyg;#1 -manifest $manifest"
                    echo "$errout"
                    exit 1
                endif
            endif
        end
    endif
    exit 0

endif


#
# Mac and Linux: add lib to front of name
#
foreach i ($qtlibs)  
    if (-d $QTDIR/lib/$i.framework) then

        # A Framework 
        cp -R $QTDIR/lib/$i.framework $instdir/$qtlibdir
        \find $instdir/$qtlibdir/$i.framework -depth -name Headers -exec \rm -rf '{}' \;
    else
        set files = `\find $QTDIR/lib -name "lib${i}*${dllext}*" -print | grep -v debug`
        if ("$files" == "") then
            echo "INSTALLQTLIB CANNOT FIND ANY lib${i}.${dllext} in $QTDIR/lib"
            exit 1
        endif
        \cp -R $files $instdir/$qtlibdir 
    endif
end
exit 0

#
#  $Log$
#  Revision 3.44  2009/05/27 05:32:06  mast
#  Create wrapper scripts only if DLLDESTDIR is defined in configure
#
#  Revision 3.43  2009/04/08 20:20:00  mast
#  Make wrapper scripts for all executables if general dll's are copied to qtlib
#
#  Revision 3.42  2009/03/20 21:50:04  mast
#  Changes for installing framework
#
#  Revision 3.41  2009/03/17 23:12:30  mast
#  No no those really were errors, back to normal single error processing on mt
#
#  Revision 3.40  2009/03/17 04:00:23  mast
#  Allow exit status 79 on mt, it is what we get now!
#
#  Revision 3.39  2009/03/17 02:24:52  mast
#  Try again to get error message
#
#  Revision 3.38  2009/03/17 00:23:05  mast
#  Ridiculous retry on mt
#
#  Revision 3.37  2009/03/16 22:18:15  mast
#  Escape echo so it works with c: stuff
#
#  Revision 3.36  2009/03/14 15:39:58  mast
#  Fixed missing endif
#
#  Revision 3.35  2009/03/14 00:03:55  mast
#  Changes for copying redistributables and adding manifests to exe's
#
#  Revision 3.34  2009/01/17 06:15:45  mast
#  Make sure Qt plugin exists before copy, and make it a+rx
#
#  Revision 3.33  2009/01/15 16:47:01  mast
#  Qt 4 port
#
#  Revision 3.31  2008/12/23 03:41:41  mast
#  UAC wrapper scripts needed to already have the final name
#
#  Revision 3.30  2008/01/26 01:59:41  mast
#  Added ability to rename some DLLs to protected names
#
#  Revision 3.29  2007/12/15 00:00:11  mast
#  Added wrappers for Vista patch executables
#
#  Revision 3.28  2006/10/19 23:42:56  mast
#  Copy libraries with wild card after as well as before the extension
#
#  Revision 3.27  2006/07/17 18:46:30  mast
#  Added Qt plugin copying
#
#  Revision 3.26  2006/06/21 21:26:38  mast
#  Generalized the dll copy logic to work for Mac, and to take names without
#  extensions, and to take destination directory
#
#  Revision 3.25  2005/02/24 04:03:32  mast
#  Needed to make directory for assistant if not there yet
#
#  Revision 3.24  2005/02/23 03:44:12  mast
#  Needed to put assistant in bin for Linux without Qt included
#
#  Revision 3.23  2004/11/22 06:06:39  mast
#  Move creation of qtlib up
#
#  Revision 3.22  2004/11/22 05:32:19  mast
#  Check for existence of qassistant lib before going through loop again
#
#  Revision 3.21  2004/11/22 00:19:51  mast
#  Added Qt assistant and library if needed
#
#  Revision 3.20  2004/06/26 02:46:51  mast
#  Needed to make all DLLs read/execute by all
#
#  Revision 3.19  2004/04/03 20:01:00  mast
#  Added ability to copy install files and rename them
#
#  Revision 3.18  2004/01/27 05:53:54  mast
#  Needed to define LIBEXT line better
#
#  Revision 3.17  2004/01/27 05:16:26  mast
#  Start out by copying needed files from dist
#
#  Revision 3.16  2003/12/30 17:24:58  mast
#  Delete stray .libs in windows install
#
#  Revision 3.15  2003/12/02 02:07:08  mast
#  Change the way Qt lib is identified in Windows, give up on Makefiles
#
#  Revision 3.14  2003/11/26 21:48:08  mast
#  Allow large number of dll lists to copy
#
#  Revision 3.13  2003/10/25 16:25:59  mast
#  fix for Cygwin/Intel and add ability to copy other libraries
#
#  Revision 3.12  2003/10/25 02:52:27  mast
#  find library in cygwin/Intel makefile
#
#  Revision 3.11  2003/10/03 05:04:45  mast
#  Discovered extension can go before (SGI) or after (Mac) version numbers
#
#  Revision 3.10  2003/09/25 00:04:08  mast
#  Improve test for no entry in configure
#
#  Revision 3.9  2003/08/25 18:47:13  mast
#  Have it make the install directory if needed, and delete existing links
#
#  Revision 3.8  2003/08/13 20:01:41  mast
#  Have it use LIBEXT from configure if available, add test for libqt.la
#
#  Revision 3.7  2003/05/08 23:29:09  mast
#  change for windows
#
#  Revision 3.6  2003/05/08 21:42:35  mast
#  Get libqt name better
#
#  Revision 3.5  2003/05/08 20:58:43  mast
#  Initial creation (deleted messages while thrashing
#
#
