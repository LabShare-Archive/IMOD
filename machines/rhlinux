#!/bin/csh
#############################################################################
# Intel running Red Hat Linux 7-8
# Modified for Intel running Red Hat 7.3  7/18/02 by dnm and rjg
#
# 3/28/03: eliminated all X includes and libs
#
set c_includes =  ' -I$(QTDIR)/include'

echo 'LDFLAGS = -L$(LIBDIR)' >> $configure

# DNM 8/27/01: Needed for large files to work right.  Use a -D define instead
# of #define in imodconfig because it must precede all system includes.
set defines = -D_FILE_OFFSET_BITS=64

if ($compiler == intel) then
	echo "CC        = icc"              >> $configure
	echo "FC        = ifc"              >> $configure
	set usfftdir = $source_dir/USFFTlib/pc-intel
else
	echo "CC        = gcc"              >> $configure
	echo "FC        = f77"              >> $configure
	if ($m64bit == false) set usfftdir = $source_dir/USFFTlib/pc
	#
	# tell the C routines being called by Fortran that they need two __
	#
	echo '#define G77__HACK' >> $hconfigure
endif

# Needed under tcsh 6.12 in RH 8.0 to avoid getting two \ out
cat << EOF >> $configure
CP        = \\cp
FIND      = \\find
EOF

#
# Set up special flags for 64 bit
#
set m64flags =
set libflags =
if ($m64bit == true) then
    set m64flags = "-m64"
    set libflags = "-fPIC"
    set include_flags = "$include_flags -I/usr/lib64"
endif

set wrapper = ""

if ($shared_libs != false) then
	echo "AR      = g++"                 >> $configure
	echo "ARCPP   = g++"                >> $configure
	echo "ARFLAGS = $flags $libflags -shared -o "    >> $configure
	echo "RANLIB    = true"                     >> $configure
	echo "LIBEXT  = so"                 >> $configure
	#
	# With Intel compiler, call an ld wrapper to strip out the -static flag
	# make blockio into a static libary  to drag in stat64 properly, and 
	# exclude it from libim.so
	#
	if ($compiler == intel) then
		set wrapper = "-Qlocation,link,$source_dir/scripts"
		set blockio = libblockio.a
		set imsubsobj = '[^b]*.o'
	endif
else
	echo "AR      = ar"                 >> $configure
	echo "ARCPP   = ar"                 >> $configure
	echo "ARFLAGS = ruv "                >> $configure
	echo "RANLIB    = ranlib"            >> $configure
	echo "LIBEXT  = a"                  >> $configure
endif

echo 'MAKELIB   = $(AR) $(ARFLAGS)' >> $configure
echo 'MAKELIBCPP = $(ARCPP) $(ARFLAGS)' >> $configure
echo "MAKELIBSO = g++ $flags -shared -o " >> $configure


echo "CLIBS   = -lc -lm" >> $configure
echo 'LIBS    = $(CLIBS)'  >> $configure
echo 'FORTCLIBS = '  >> $configure

if ($compiler == intel) then
	# For Intel, add -lXp, add -Vaxlib flag for compatibility calls
	echo "IMLIBS     = $wrapper -Vaxlib -lhvem -lim -lblockio -lifft  -limod -lrandm -ldtrigs " >> $configure
else
	echo "IMLIBS     = -lhvem -lim -lifft  -limod -lrandm -ldtrigs" >> $configure
endif

echo 'IMODCC    = $(CC)'              >> $configure
echo  'MAKEF77PLUGIN  = $(FC) '"$wrapper"' -shared $(LDFLAGS) -ldtrigs -o'  >> $configure
echo 'FLCC      = $(FC)'              >> $configure
echo "F77LIBS   = $wrapper"                  >> $configure

#
# It said "Needed for Red Hat 9/Qt 3.1" but now it seems not, and gcc 4 needs
# it gone
#set fort_cpplibs = -lstdc++

# 
# tell the C routines to swap the bytes for INTEL
#
echo '#define B3D_LITTLE_ENDIAN' >> $hconfigure
echo '#define SWAP_IEEE_FLOATS' >> $hconfigure

#
# Tell fortran routines how to swap bytes too
#
\cp $endian_dir/little_endian.inc $endian_dir/endian.inc

#
#
# tell zap window to redraw twice after a resize (GeForce3/RH7.1 problem)
#
#echo '#define ZAP_RESIZE_HACK'  >> $hconfigure
#
# The following hacks were needed for Nvidia 0.95 drivers
#
# tell imod to not count on GL_LINE_LOOP working
#echo '#define LINE_LOOP_HACK'  >> $hconfigure
# tell imod to fill all the way across lines when drawing pixels
#echo '#define PIXELDRAW_HACK'  >> $hconfigure
# tell imod to clear top of xyz window after draw
#echo '#define XYZ_CLEAR_HACK'  >> $hconfigure
# tell imod to draw images in chunks
#echo '#define CHUNKDRAW_HACK 4'  >> $hconfigure
#
# If using Mesa libGLU, tell imod that hacks are needed for contour fill by
# tesselation
#
#echo '#define TESS_HACK'  >> $hconfigure
#
# tell midas to time out expose events in montage fixing mode, and imod
# to do it generally
#
#echo '#define MIDAS_EXPOSE_HACK'  >> $hconfigure
#echo '#define ZAP_EXPOSE_HACK'  >> $hconfigure
#
# Use fixed CLK_TCK so code compiled on 7.1 runs on 7.0
#
#echo '#define FIXED_CLK_TCK 100'  >> $hconfigure

#
# Use a workaround when window managers won't do StaysOnTop reliably
#
echo '#define STAY_ON_TOP_HACK' >> $hconfigure

# For Qt 3.2 onward, need to post message twice
#
echo '#define SENDEVENT_RETRY_HACK 0' >> $hconfigure

if ($compiler == intel) then
	## -axK fails on tiltalign-beadtrack
	set optimization = "-O2 -unroll -w"
	#set optimization = "-O2 -w"
	if ($debug == true) set optimization = "-g -O0"
else
	set optimization = "-O3 -w"
	if ($debug == true) set optimization = "-ggdb -O0"
endif

# 4/8/04: Use intermediate variables to simplify all the defines that are needed
#
set cflags = "$flags $m64flags $optimization $defines $include_flags $c_includes"
set fflags = "$flags $m64flags $optimization $include_flags"

echo "CFLAGS  = $cflags" >> $configure
echo "CXXFLAGS = $cflags" >> $configure
echo "FFLAGS  = $fflags" >> $configure
echo "NOOPFFLAGS = $fflags -O0" >> $configure

# 4/8/04: Add flags for building libraries
#
echo "LIBCFLAGS  = $libflags $cflags" >> $configure
echo "LIBFFLAGS  = $libflags $fflags" >> $configure

#
#
#  The fallbacks need to be defined because of problems with n32 code on the
#  SGI; just define them the same as non-fallback versions.
#
echo "CFALLBACK  = $cflags" >> $configure
echo "FFALLBACK  = $fflags" >> $configure
echo 'LDFALLBACK = $(LDFLAGS)'       >> $configure
echo 'NOOPFFALLBACK = $(NOOPFFLAGS)' >> $configure

if ($m64bit == true) then
    # If 64-bit, use fortran routines for tilt
    echo 'TILTOBJS = bpsumnox.o bpsumxtilt.o bpsumlocal.o' >> $configure
else
    # Otherwise use assembly code for tilt program
    echo 'TILTOBJS = handnox.o handxtilt.o handlocal.o' >> $configure
endif

# make extra fortran libs
#
echo 'LINUXFLIBS = $(LIBDTRIGS) $(LIBRANDM)' >> $configure

# Qtplax needs to kill second thread on Linux, but problem in Suse 64-bit
#
if ($m64bit == false) echo '#define QTPLAX_ATEXIT_HACK' >> $hconfigure

#
#   default is to assume tifflib exists
#
if ($tifflibs == default) set tifflibs = true

#
# Set the switch point include file for the FBP algorithm in Tilt
#
set fbpswitch       = fbpswitch-pc.inc

#
# Set up things for Qt include files
#
cat << EOF  >! $midas_qconf
DEFINES	+= _FILE_OFFSET_BITS=64
EOF

cat << EOF >! $imod_qconf
SOURCES += linegui.cpp
HEADERS += linegui.h
LIBS += -L$lib_dir -liimod -limod -ldiaqt -lifft -ltrack -ltiff 
LIBS += -lqassistantclient -lm
DEFINES	+= _FILE_OFFSET_BITS=64
QMAKE_LFLAGS_DEBUG += -Wl,-E 
QMAKE_LFLAGS_RELEASE += -Wl,-E
EOF

if ($packqt == true) then
    #
    # Set up to copy qt lib for distribution, set up for correctly named
    # startup files depending on whether it is default to use Qt or not
    #
    echo 'QTINSTLIBDIR = qtlib' >> $configure
    if ($useqt == true) then
        echo 'INSTALL_RENAME = IMOD-linuxPackQt.csh IMOD-linux.csh IMOD-linuxPackQt.sh IMOD-linux.sh' >> $configure
    else
        echo 'INSTALL_RENAME = IMOD-linuxPackQt.csh IMOD-localQt.csh IMOD-linuxPackQt.sh IMOD-localQt.sh' >> $configure
        echo 'INSTALL_FILES = IMOD-linux.csh IMOD-linux.sh' >> $configure
    endif
else
    echo 'INSTALL_FILES = IMOD-linux.csh IMOD-linux.sh IMOD-qtconflict.csh IMOD-qtconflict.sh' >> $configure
endif
