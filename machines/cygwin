#!/bin/csh
#############################################################################
# Intel running Cygwin/Windows
#
#
#  Separate pointers are needed for X and Motif includes
#
set Xlibdir = /usr/X11R6/lib
set Xincdir = /usr/X11R6/include/X11
set Mincdir = /usr/X11R6/include
if ($no_xlibs == false) then
    if (!(-e $Xlibdir) || !(-e $Xincdir)) set no_xlibs = true
endif

if ($no_xlibs == false) then
    set c_includes =  "-I$Xincdir -I$Mincdir"' -I$(QTDIR)/include'
    echo 'LDFLAGS = -L$(LIBDIR)' "-L$Xlibdir" >> $configure
    echo 'XPLAX = plax.o' >> $configure
else
    set c_includes =  '-I$(QTDIR)/include'
    echo 'LDFLAGS = -L$(LIBDIR)' >> $configure
    echo 'XPLAX = noplax.o' >> $configure
endif

# DNM 8/27/01: Needed for large files to work right.  Use a -D define instead
# of #define in imodconfig because it must precede all system includes.
set defines = -D_FILE_OFFSET_BITS=64

echo "SHELL     = /bin/csh"         >> $configure

if ($compiler == intel) then
	echo "CC        = icc"              >> $configure
	echo "FC        = ifc"              >> $configure
	set usfftdir = $source_dir/USFFTlib/pc-intel
else
	echo "CC        = gcc"              >> $configure
	echo "FC        = g77"              >> $configure
	#
	# tell the C routines being called by Fortran that they need two __
	#
	echo '#define G77__HACK' >> $hconfigure
endif

# Needed under tcsh 6.12 in RH 8.0 to avoid getting two \ out
cat << EOF >> $configure
CP        = \\cp
FIND      = \\find
EOF

set shared_libs = false

set wrapper = ""

if ($shared_libs != false) then
	echo "AR      = ld"                 >> $configure
	echo "ARCPP   = g++"                >> $configure
	echo "ARFLAGS = $flags -shared -o"    >> $configure
	echo "RANLIB    = true"                     >> $configure
	echo "LIBEXT  = so"                 >> $configure
	#
	# With Intel compiler, call an ld wrapper to strip out the -static flag
	# make blockio into a static libary  to drag in stat64 properly, and 
	# exclude it from libim.so
	#
	if ($compiler == intel) then
		set wrapper = "-Qlocation,link,$source_dir/scripts"
		set blockio = libblockio.a
		set imsubsobj = '[^b]*.o'
	endif
else
	echo "AR      = ar"                 >> $configure
	echo "ARCPP   = ar"                 >> $configure
	echo "ARFLAGS = ruv"                >> $configure
	echo "RANLIB    = ranlib"            >> $configure
	echo "LIBEXT  = a"                  >> $configure
endif

echo 'MAKELIB   = $(AR) $(ARFLAGS)' >> $configure
echo 'MAKELIBCPP = $(ARCPP) $(ARFLAGS)' >> $configure
echo "MAKELIBSO = g++ $flags -shared -o" >> $configure


echo "CLIBS   = -lc -lm" >> $configure
echo 'LIBS    = $(CLIBS)'  >> $configure
echo 'FORTCLIBS = '  >> $configure

if ($compiler == intel) then
	# For Intel, add -lXp, add -Vaxlib flag for compatibility calls
	echo "XLIBS   = -lXm -lXp -lXt -lSM -lICE -lXext -lX11" >> $configure
	echo "IMLIBS     = $wrapper -Vaxlib -lhvem -lim -lblockio -lifft  -limod -lrandm -ldtrigs " >> $configure
else
	echo "XLIBS   = -lXm -lXt -lSM -lICE -lXext -lX11" >> $configure
	echo "IMLIBS     = -lhvem -lim -lifft  -limod -lrandm -ldtrigs" >> $configure
endif
if ($no_xlibs == false) then
        echo 'GRAPHLIBS    = -ldnmncar $(IMLIBS) $(XLIBS)' >> $configure
else
        echo 'GRAPHLIBS    = -ldnmncar $(IMLIBS)' >> $configure
endif

# 
# If using plugins, need to tell linker to export symbols
echo 'IMODCC    = $(CC)'              >> $configure
echo  'MAKEF77PLUGIN  = $(FC) '"$wrapper"' -shared $(LDFLAGS) -ldtrigs -o'  >> $configure
echo 'FLCC      = $(FC)'              >> $configure
echo "F77LIBS   = $wrapper"                  >> $configure

# 
# tell the C routines to swap the bytes for INTEL
#
echo '#define B3D_LITTLE_ENDIAN' >> $hconfigure
echo '#define SWAP_IEEE_FLOATS' >> $hconfigure

echo '#define NO_IMOD_FORK' >> $hconfigure
echo '#define CLIPBOARD_TIMER_HACK 100' >> $hconfigure


#
# Tell fortran routines how to swap bytes too, and set # of bytes per
# item to 1 for defining record length in fortran direct unformatted I/O
#
\cp $endian_dir/little_endian.inc $endian_dir/endian.inc
echo "      parameter (nbytes_recl_item=1)" >! $endian_dir/recl_bytes.inc


if ($compiler == intel) then
	## -axK fails on tiltalign-beadtrack
	set optimization = "-O2 -unroll -w"
	#set optimization = "-O2 -w"
	if ($debug == true) set optimization = "-g -O0"
else
	set optimization = "-O3 -w"
	if ($debug == true) set optimization = "-ggdb -O0"
endif

#
#
#  The fallbacks need to be defined because of problems with n32 code on the
#  SGI; just define them the same as non-fallback versions.
#
echo "CFLAGS  = $flags $optimization $defines $include_flags $c_includes" >> $configure
echo "CXXFLAGS = $flags $optimization $defines $include_flags $c_includes" >> $configure
echo "FFLAGS  = $flags $optimization $include_flags" >> $configure
echo "NOOPFFLAGS = $flags $optimization -O0 $include_flags" >> $configure

echo "CFALLBACK  = $flags $optimization $defines $include_flags $c_includes" >> $configure
echo "FFALLBACK  = $flags $optimization $include_flags" >> $configure
echo 'LDFALLBACK = $(LDFLAGS)'       >> $configure
echo 'NOOPFFALLBACK = $(NOOPFFLAGS)' >> $configure

# Use fortran code for tilt program, make extra fortran libs, skip SGI progs
echo 'TILTOBJS = bpsumnox.o bpsumxtilt.o bpsumlocal.o' >> $configure
echo 'LINUXFLIBS = $(LIBDTRIGS) $(LIBRANDM)' >> $configure
echo 'RGBPROGS = '  >> $configure
#
#   default is to assume tifflib exists
#
if ($tifflibs == default) set tifflibs = true

#
# Set the switch point include file for the FBP algorithm in Tilt
#
set fbpswitch       = fbpswitch-pc.inc

#
# Start Qt include files
#
echo " " >! $midas_qconf
cat << EOF >! $imod_qconf
INCLUDEPATH += ..\include .
LIBS += libdiaqt.lib libimod.lib libiimod.lib
QMAKE_LFLAGS_RELEASE += /LIBPATH:..\buildlib /NODEFAULTLIB:libc.lib /NODEFAULTLIB:libcd.lib /NODEFAULTLIB:msvcrtd.lib
QMAKE_LFLAGS_DEBUG += /LIBPATH:..\buildlib /NODEFAULTLIB:libc.lib /NODEFAULTLIB:libcd.lib /NODEFAULTLIB:msvcrt.lib
EOF

if ($debug == true) then
    echo CONFIG += debug warn_off >> $imod_qconf
    set vcexedir = Debug
else
    echo CONFIG += release warn_off >> $imod_qconf
    set vcexedir = Release
endif

echo VCEXEDIR = $vcexedir >> $configure

# set up make files for things built by VisualC
set makefile_ext = dummy
set makefile_dirs = ($imod_dir $midas_dir $sendevent_dir $plugsrc_dir $libdiaqt_dir)

#
# Set up to copy qt lib to bin dir for a distribution
echo 'QTINSTLIBDIR = bin' >> $configure

#
# Add scripts for running from command line
echo '3dmod $* &' >! $imod_dir/3dmodbg
echo '3dmod -view $* &' >! $imod_dir/3dmodv

# Make sure critical scripts are in unix mode
(cd scripts ; dos2unix copytomocoms setupcombine makejoincom transferfid >& /dev/null)
