#!/bin/csh -f
#
# Script to convert .preman files to .man files, and .man files to .1 files
# then to .html files,
#
#  $Author$
#
#  $Date$
#
#  $Revision$
#
#  $Log$
#  Revision 3.5  2006/02/10 02:52:48  mast
#  Added program name argument to mantohtml
#
#  Revision 3.4  2003/10/29 18:25:20  mast
#  Add -Tascii flag for RH8 and RH9
#
#  Revision 3.3  2003/10/08 17:19:22  mast
#  Changes to work with autodoc files
#
#  Revision 3.1  2003/10/08 17:17:43  mast
#  Changes to work with autodoc files

set version = `cat ../.version`
set instext = "INSERT OPTION"

set prelist = `\find . -type f -name '*.preman'`

if ($argv[1] == "clean") goto clean

which nroff >& /dev/null
if ($status) then
    echo nroff not on path
    exit 1
endif

# test for -c option, needed starting in Red Hat 8
set copt = "-c -Tascii"
echo junk | nroff -c -Tascii >& /dev/null
if ($status) set copt = ""

@ ind = 1
while ($ind <= $#prelist)
    set i = $prelist[$ind]
    set manout = ${i:r}.man
    grep "$instext" $i >& /dev/null
    if ($status) then
        echo "$instext, the keyword for text to be replaced, is missing in $i"
        exit 1
    endif
    echo "converting $i to $manout"
    awk "NR==1, /$instext/" $i | grep -v "$instext" >! $manout
    $argv[1]/autodoc2man 1 1 ${i:r} >> $manout
    if ($status) then
        echo "Error getting manpage text from option file for ${i:r}"
        exit 1
    endif
    awk "/$instext/, /GobbledyGook/" $i | grep -v "$instext" >> $manout
    @ ind++
end    

foreach i (*.man)
    echo "converting $i to ${i:r}.1"
    sed -e "/^\.TH/s/ [0-9]\.[0-9\.]* / $version /" \
        -e "/\([^ ]*\)@colorado.edu/s//\1 at colorado dot edu/g" $i \
        | nroff -man $copt >! ${i:r}.1
end

foreach j (*.1)
	echo "converting $j to ${j:r}.html"
	$argv[1]/mantohtml $j:t:r < $j >! ${j:r}.html
end

clean:
@ ind = 1
while ($ind <= $#prelist)
    \rm -f ${prelist[$ind]:r}.man
    @ ind++
end

exit

