#!/bin/csh -f
# Script to package fortran graphics programs in individual .app directories
#
#  $Id$

#
# get the install directory and copy .version to VERSION unconditionally
#
set instdir = `sed -n '/INSTDIR/s/^.*= *//p' configure`
\cp .version $instdir/VERSION

#
# Get the list of programs needing packaging
#
set fprogs = `sed -n '/MACAPPS/s/^.*= *//p' configure`

if ($#fprogs == 0) exit 0
if ($fprogs[1] == "") exit 0

#
# get the sendevent contents directory
#
set imsecont = qttools/sendevent/imodsendevent.app/Contents

#
# Loop on programs, copy PkgInfo, and Info.plist with possible name change
#
foreach prog ($fprogs)
    if (-e $instdir/bin/$prog) then
        set contents = $instdir/bin/${prog}.app/Contents
        if (! -e $contents/MacOS) mkdir -p $contents/MacOS
        \cp $imsecont/PkgInfo $contents
        if (-e $imsecont/Info.plist) then
            sed "/imodsendevent/s//$prog/" < $imsecont/Info.plist > $contents/Info.plist
        endif
        \mv $instdir/bin/$prog $contents/MacOS
        if (-e $instdir/bin/run$prog) \mv $instdir/bin/run$prog $instdir/bin/$prog
    endif
end

