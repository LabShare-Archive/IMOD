#!/bin/csh -f
# Script to package fortran graphics programs in individual .app directories
#
#  $Id$
#  Log at end of file

#
# get the install directory and copy .version to VERSION unconditionally
#
set instdir = `sed -n '/INSTDIR/s/^.*= *//p' configure`
\cp .version $instdir/VERSION

#
# Get the list of programs needing packaging
#
set fprogs = `sed -n '/MACAPPS/s/^.*= *//p' configure`

if ($#fprogs == 0) exit 0
if ($fprogs[1] == "") exit 0

#
# get the sendevent contents directory
#
set imsecont = sendevent/imodsendevent.app/Contents

#
# Loop on programs, copy PkgInfo, and Info.plist with possible name change
#
foreach prog ($fprogs)
    if (-e $instdir/bin/$prog) then
        set contents = $instdir/bin/${prog}.app/Contents
        if (! -e $contents/MacOS) mkdir -p $contents/MacOS
        \cp $imsecont/PkgInfo $contents
        if (-e $imsecont/Info.plist) then
            sed "/imodsendevent/s//$prog/" < $imsecont/Info.plist > $contents/Info.plist
        endif
        \mv $instdir/bin/$prog $contents/MacOS
        if (-e $instdir/bin/run$prog) \mv $instdir/bin/run$prog $instdir/bin/$prog
    endif
end

#
# Convert Ctrl to Apple in selected files if this is not the install directory
#
if (`pwd` == $instdir) exit 0
foreach i ($instdir/html/3dmodguide.html $instdir/html/3dmodHelp/*.html \
    $instdir/html/midasHelp/*.html)
    sed '/Ctrl/s//Apple/g' < $i >! ctrltmp
    \mv ctrltmp $i
end
\rm -f ctrltmp

#
#  $Log$
#  Revision 1.13  2009/04/09 04:35:16  mast
#  With Intel C compiler, no Info.plist files, so test for that
#
#  Revision 1.12  2009/01/15 16:47:01  mast
#  Qt 4 port
#
#  Revision 1.10  2009/01/02 06:50:14  mast
#  Add wrapper for assistant
#
#  Revision 1.9  2008/03/03 17:46:15  mast
#  Rename run scripts to program names
#
#  Revision 1.8  2007/10/03 22:36:17  mast
#  Added Ctrl conversion for midas help files
#
#  Revision 1.7  2006/06/22 01:10:35  mast
#  Removed libary copying; make installqtlib do it
#
#  Revision 1.6  2005/12/09 20:56:00  mast
#  Generalized fortran library copying to take a variable and list
#
#  Revision 1.5  2005/06/10 19:48:03  mast
#  Copied libg2c.dylib from /usr/local/lib to IMOD/lib
#
#  Revision 1.4  2004/11/24 05:13:27  mast
#  Added conversion of Ctrl to Apple in selected 3dmod html documents
#
#  Revision 1.3  2004/08/31 02:24:05  mast
#  Added copying of .version to VERSION
#
#  Revision 1.2  2003/09/25 00:03:26  mast
#  Fix test for empty entry
#
#  Revision 1.1  2003/09/23 21:10:37  mast
#  Initial creation
#
