#!/bin/csh -f
# Script to package fortran graphics programs in individual .app directories
#
#  $Author$
#
#  $Date$
#
#  $Revision$
#
#  $Log$
#  Revision 1.5  2005/06/10 19:48:03  mast
#  Copied libg2c.dylib from /usr/local/lib to IMOD/lib
#
#  Revision 1.4  2004/11/24 05:13:27  mast
#  Added conversion of Ctrl to Apple in selected 3dmod html documents
#
#  Revision 1.3  2004/08/31 02:24:05  mast
#  Added copying of .version to VERSION
#
#  Revision 1.2  2003/09/25 00:03:26  mast
#  Fix test for empty entry
#
#  Revision 1.1  2003/09/23 21:10:37  mast
#  Initial creation
#

#
# get the install directory and copy .version to VERSION unconditionally
#
set instdir = `sed -n '/INSTDIR/s/^.*= *//p' configure`
\cp .version $instdir/VERSION

#
# Get the list of programs needing packaging
#
set fprogs = `sed -n '/MACAPPS/s/^.*= *//p' configure`

if ($#fprogs == 0) exit 0
if ($fprogs[1] == "") exit 0

#
# get the sendevent contents directory
#
set imsecont = sendevent/imodsendevent.app/Contents

#
# Loop on programs, copy PkgInfo, and Info.plist with possible name change
#
foreach prog ($fprogs)
    set contents = $instdir/bin/${prog}.app/Contents
    if (! -e $contents/MacOS) mkdir -p $contents/MacOS
    \cp $imsecont/PkgInfo $contents
    sed "/imodsendevent/s//$prog/" < $imsecont/Info.plist > $contents/Info.plist
    \mv $instdir/bin/$prog $contents/MacOS
end

#
# Convert Ctrl to Apple in selected files if this is not the install directory
#
if (`pwd` == $instdir) exit 0
foreach i ($instdir/html/3dmodguide.html $instdir/html/3dmodHelp/*.html)
    sed '/Ctrl/s//Apple/g' < $i >! ctrltmp
    \mv ctrltmp $i
end
\rm -f ctrltmp

#
# Copy specified fortran library(s) if it exists (-R makes it simple)
#
set fortlibs = `sed -n '/COPYFORTLIB/s/^.*= *//p' configure`
if ("$fortlibs" == "") exit
foreach i ($fortlibs) 
    if (-e /usr/local/lib/$i.dylib) \
    \cp -R /usr/local/lib/$i.*dylib $instdir/lib
end
