#!/bin/csh -f
# A simple script to kill a process group given the PID of one member of the
# group.  Given multiple PIDs it will kill all the groups with one ps command
#
#  $Id$
#

if ($#argv < 1) then
    echo "ERROR: imodkillgroup - no PID provided as argument 1"
    exit 1
endif

set system = `uname -s`
set windows = 0

# Get PID and PGID in two column output for each system, for all lines 
# containing the PID somewhere
#
switch ($system)
    case *IRIX*:
        set proclist = `ps -ej | awk '{print $1, $2}'`
        breaksw
    case *Linux*:
        set proclist = `ps -eo "%p %r"`
        breaksw
    case *CYGWIN*:

        # Strip any leading characters like S for suspended
        set proclist = `ps -el | sed -e '/^[^ ]/s///' | awk '{print $1, $3}'`
        set windows = 1
        breaksw
    case *Darwin*:
        set proclist = `ps -jA | awk '{print $2, $4}'`
        breaksw
    default:
        echo "ERROR: imodkillgroup - system $system not supported"
        exit 2
endsw

set retval = 0
while ($#argv > 0)
    set pid = $argv[1]
    shift

    # Search for the PID and kill by the PGID
    #
    @ i = 0
    set found = 0
    while ($i < $#proclist - 1)
        @ i++
        if ("$proclist[$i]" == "$pid") then

            # For some reason it didn't like $proclist[$i+1]; "missing -"
            @ j = $i + 1

            # It turns out that tcsh kill works fine with a pgid too.
            # Cygwin kill now complains about "-9: no such process" even though it
            # works, so just use the (silent) built-in kill instead
            # Neither tcsh nor bash man page mention that group kills work, and the
            # kill man page on cygwin does not either, but they all work.
            if ($windows) then
                kill -9 -$proclist[$j]
            else
                \kill -9 -$proclist[$j]
            endif
            set found = 1
            break
        endif
    end
    if (! $found) then
        echo "imodkillgroup - no process found with PID $pid"
        set retval = 3
    endif
end

exit $retval

#  $Log$
#  Revision 3.5  2010/12/31 15:45:00  mast
#  Made it do multiple PIDs
#
#  Revision 3.4  2010/11/02 16:38:07  mast
#  remove DOS endings (?)
#
#  Revision 3.3  2010/11/02 16:35:01  mast
#  Fix for cygwin killing suspended process
#
#  Revision 3.2  2006/10/09 19:33:20  mast
#  Remove ERROR: tag when it can't find the process
#
#  Revision 3.1  2005/08/24 16:04:38  mast
#  Addition to package
#
