#!/bin/csh -f
# A simple script to kill a process group given the PID of one member of the
# group
#
#  $Author$
#
#  $Date$
#
#  $Revision$
#
#  $Log$
#  Revision 3.1  2005/08/24 16:04:38  mast
#  Addition to package
#
#

if ($#argv < 1) then
    echo "ERROR: imodkillgroup - no PID provided as argument 1"
    exit 1
endif

set pid = $argv[1]
set system = `uname -s`

# Get PID and PGID in two column output for each system, for all lines 
# containing the PID somewhere
#
switch ($system)
    case *IRIX*:
        set proclist = `ps -ej | grep $pid | awk '{print $1, $2}'`
        breaksw
    case *Linux*:
        set proclist = `ps -eo "%p %r" | grep $pid`
        breaksw
    case *CYGWIN*:
        set proclist = `ps -el | grep $pid | awk '{print $1, $3}'`
        breaksw
    case *Darwin*:
        set proclist = `ps -jA | grep $pid | awk '{print $2, $4}'`
        breaksw
    default:
        echo "ERROR: imodkillgroup - system $system not supported"
        exit 2
endsw

# Search for the PID and kill by the PGID
#
while ($#proclist >= 2)
    if ("$proclist[1]" == "$pid") then
        \kill -9 -$proclist[2]
        exit 0
    endif
    shift proclist ; shift proclist
end
echo "imodkillgroup - no process found with PID $pid"
exit 3
