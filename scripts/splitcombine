#! /bin/csh -f
# splitcombine: chops up volcombine.com to run on multiple machines
#
#  $Id$
#  Log at end of file
#

set pn = splitcombine

set tmpdir = ""

set comname = volcombine.com
if ($#argv > 0) then
    if ("$argv[1]" == "-h") then
        cat <<EOF
Usage: splitcombine [command_file] [temporary_directory]
  Generates command files for running volcombine operation on multiple machines
  First optional argument is name of command file (default volcombine.com)
  Second optional argument is temporary directory to use (default $tmpdir)
EOF
        exit 0
    endif

    set comname = $argv[1]
endif

if ($#argv > 1) then
    set tmpdir = `echo $argv[2] | sed '/\//s//\\\//g'`
endif


set tmprec = \$tmpdir\\/rec.
set tmpmat = \$tmpdir\\/mat.

set rootname = $comname:r
set comname = $rootname.com

if (! -e $comname) then
    echo "ERROR: $pn - Command file $comname does not exist"
    exit 1
endif

# Make sure directory for sum*.rec is writable
set sum1rec = `sed -n '/.*clip.*\([^ ]*sum1.rec\)/s//\1/p' $comname`
set sumdir = "."
if ("$sum1rec" != "") then
    set sumdir = "$sum1rec:h"
    if ("$sumdir" == "$sum1rec") set sumdir = "."
endif
if (! -w "$sumdir") then
    echo "ERROR: $pn - Unable to write sum*.rec to directory $sumdir"
    exit 1
endif

# Figure out how many pieces
#
set numpieces = `sed -n -e '/[[:cntrl:]]/s///g' -e '/^.*COMBINING PIECE *1 *of /s///p' $comname`
@ numpieces = $numpieces

set optionline = `grep 'set *combinefft_red.*=' $comname`
set optionline2 = `grep 'set *combinefft_low.*=' $comname`
set optionline3 = `grep 'setenv IMOD_BRIEF_HEADER' $comname`

set usingtmp = `grep '\/usr\/tmp' $comname`
if ("$usingtmp" != "") then
    echo "WARNING: This command file accesses /usr/tmp and will not "
    echo "run on multiple machines"
else
    set usingtmp = `grep '\/tmp' $comname`
    if ("$usingtmp" != "") then
        echo "WARNING: This command file accesses /tmp and may not "
        echo "run on multiple machines"
    endif
endif

# Set up to replace localscratch with scratch/host
#
set thishost = `hostname`
set hostroot = $thishost:r:r:r

awk "/^# THIS FILE/, /COMBINING PIECE *1/" $comname | \
 sed '/COMBINING PIECE/d' >! $rootname-start.com

@ num = 0
while ($num < $numpieces)
    @ num++
    set numtext = $num
    if ($num < 10) then
        set numtext = 00"$numtext"
    else if ($num < 100) then
        set numtext = 0"$numtext"
    endif
    set thiscom = $rootname-$numtext.com
    cat <<EOF >! $thiscom
\$set tmpext = \`hostname\`.\$\$
EOF
    if ($tmpdir == "") then
        cat <<EOF >> $thiscom
\$set tmpdir = /usr/tmp
\$if (\$?IMOD_DIR) then
\$if (-e "\$IMOD_DIR/bin/settmpdir") source "\$IMOD_DIR/bin/settmpdir"
\$endif
EOF
    else
        cat <<EOF >> $thiscom
\$set tmpdir = "$tmpdir"
EOF
    endif
    
    if ("$optionline" != "") echo $optionline >> $thiscom
    if ("$optionline2" != "") echo $optionline2 >> $thiscom
    if ("$optionline3" != "") echo $optionline3 >> $thiscom
    @ nextnum = $num + 1
    set tmpname = $rootname.tmp.$$
    if ($num < $numpieces) then
        awk "/COMBINING PIECE *$num /, /COMBINING PIECE *$nextnum /" $comname >! $tmpname
    else
        awk "/COMBINING PIECE *$num /, /REASSEMBLING PIECES/" $comname >! $tmpname
    endif

    # doctor the filenames.  Need to replace all leading paths before rec. and
    # mat. to get rid of temporary directory.  Match all back to space or tab
    # But need to put escapes in front of the $tmpdir entries at start of line
    sed -e '/STATUS:/d' \
        -e "/localscratch/s//scratch\/$hostroot/g" \
        -e '/rec\.st/s//rec.st.\$tmpext/g' \
        -e '/mat\.st/s//mat.st.\$tmpext/g' \
        -e '/rec\.fft/s//rec.fft.\$tmpext/g' \
        -e '/mat\.fft/s//mat.fft.\$tmpext/g' \
        -e "/^[^ 	]*rec\./s//\\$tmprec/g" \
        -e "/^[^ 	]*mat\./s//\\$tmpmat/g" \
        -e "/[ 	][^ 	]*rec\./s// $tmprec/g" \
        -e "/[ 	][^ 	]*mat\./s// $tmpmat/g" \
        < $tmpname >> $thiscom
    \rm -r $tmpname
end

awk '/REASSEMBLING PIECES/, /if \(-e/' $comname >! $rootname-finish.com
cat <<EOF >> $rootname-finish.com
\$\\rm -f $rootname-[0-9]*.*
EOF
@ nextnum++
echo "$nextnum command files created and ready to run with:"
echo "  processchunks machine_list $rootname"

#
#  $Log$
#  Revision 3.13  2008/05/08 00:08:26  mast
#  Do not escape quotes in a cat!
#
#  Revision 3.12  2008/01/07 22:44:13  mast
#  Do not escape quotes in a cat, only dollars
#
#  Revision 3.11  2008/01/04 07:03:38  mast
#  Fixed for space in IMOD_DIR, possibly for space in tmpdir
#
#  Revision 3.10  2006/10/06 19:24:36  mast
#  Renamed the variable
#
#  Revision 3.9  2006/10/05 17:46:05  mast
#  copied BRIEF_HEADER line too
#
#  Revision 3.8  2006/09/13 23:51:30  mast
#  Handle second combinefft line
#
#  Revision 3.7  2006/06/06 22:33:39  mast
#  Needed to strip control chars before /p
#
#  Revision 3.6  2006/02/16 06:47:09  mast
#  Stripped control chars from output of sed/header etc for Windows
#
#  Revision 3.5  2005/07/24 17:03:41  mast
#  Made tmpdir be a variable in the command file and had it work appropriately
#  for the machine running the file
#
#  Revision 3.4  2004/11/30 04:43:43  mast
#  Changed to fit new combinefft_reduce variable
#
#  Revision 3.3  2004/09/29 16:07:40  mast
#  Added check for ability to write to directory where sum*.rec are placed
#
#  Revision 3.2  2004/08/24 18:48:03  mast
#  Fixed problem with needing to replace local temporary directory
#  on specifications of rec.*, mat.*
#
#  Revision 3.1  2004/07/21 04:19:41  mast
#  Initial creation
