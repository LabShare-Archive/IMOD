#!/bin/csh -f
# submfg runs a sequence of .com files and gives completion and error messages
#
#  $Author$
#
#  $Date$
#
#  $Revision$
#
#  $Log$
#  Revision 3.1  2003/04/18 22:52:21  mast
#  Initial addition to package
#
 
if ($#argv < 1) then
cat <<EOF
subm or submfg will execute a series of command files in sequence
Usage:  submfg command_file1 command_file2 ...
        Command files must have extension .com
        If the filename is comfile.com, you can enter 
                 comfile    comfile.    or  comfile.com
        submfg will execute the files in the foreground
        To run jobs in the background, set up an alias:  
               alias subm 'submfg \!* &'
        Set the environment variable SUBM_MESSAGE to modify the message upon
                completion
EOF
exit 0
endif

if ($?IMOD_DIR) set path = ($IMOD_DIR/bin $path)

set message = " finished successfully"
if ($?SUBM_MESSAGE) then
    set message = $SUBM_MESSAGE
endif
set bell = ""

while ($#argv > 0)
    set rootname = $argv[1]:r
    if (! -e $rootname.com) then
	echo ERROR: $rootname.com does not exist $bell
	exit 1
    endif
    echo -n "Running $rootname.com ...  "
    vmstocsh $rootname.log < $rootname.com | csh -ef
    if ($status) then
	echo Error executing $rootname.com $bell
	if (-e $rootname.log) tail -1 $rootname.log
	exit 1
    else
	echo $rootname.com $message
    endif
    shift
end
exit 0
