#!/bin/csh -f
# Collects all the small files for diagnosing problems building tomograms
#
#  $Author$
#
#  $Date$
#
#  $Revision$
#
#  $Log$
#  Revision 3.1  2004/10/07 16:18:22  mast
#  Initial creation
#

# Defaults
#
set pn = tomosnapshot
set curdir = .
set edffile = ""
set outfile = ""
set setname = ""
set axistype = ""

# The file lists
#
set oneAxisComLog = (eraser xcorr prenewst track align sample tomopitch tilt)
set combineComLog = (solvematch solvematchshift solvematchmod matchvol1 \
patchcorr matchorwarp volcombine combine)
set oneAxisSetFiles = (.rawtlt _peak.mod .prexf .prexg .seed .fid .3dmod .tlt \
fid.xyz .resid .resmod .tltxf _fid.xf local.xf .matmod)
set singleFiles = (solvezero.xf solve.xf refine.xf warp.xf inverse.xf \
patch.out patch_vector.mod patch_region.mod etomo_err.log etomo_err.log~ \
etomo_out.log lslrt.out uname.out)

echo "$pn : enter $pn -h for usage"
while ($#argv > 0)
    switch ($argv[1])
    case -h:
        goto usage
    case -e:
        set edffile = $argv[2]
        shift; shift
        breaksw
    case -o:
        set outfile = $argv[2]
        shift; shift
        breaksw

    default:
        if ($#argv > 1) then
            echo "ERROR: $pn - unrecognized option $argv[1]"
            exit 1
        endif
        set curdir = $argv[1]
        shift
        if (! -d $curdir) then
            echo "ERROR: $pn - argument $curdir is not a directory"
            exit 1
        endif
        breaksw
    endsw
end

# Get current directory before going to specified one
#
set rundir = `pwd`
cd $curdir

# Get edf file
#
if ("$edffile" == "") then
    set edfFiles = `ls *.edf`
    if ($status) then
        echo "WARNING: $pn - No edf file found in this directory"
    else
        set edffile = $edfFiles[1]
    endif
else if (! -e "$edffile") then
    echo "WARNING: $pn - $edffile does not exist"
    set edffile = ""
endif

# Get set name and axis type from edf file
#
if ("$edffile" != "")then
    set setname = `sed -n '/Setup.DatasetName=/s///p' $edffile`
    set axistype = `sed -n '/Setup.AxisType=/s///p' $edffile`
endif

# Get the axis type from xcorr.com as fallback
#
if ("$axistype" == "") then
    set xcorrfile = `\ls xcorr.com`
    if ($status) then
        set xcorrfile = `\ls xcorra.com`
        if ($status) then
            echo "ERROR: $pn - Cannot find AxisType in edf file or from command files"
            exit 1
        endif
        set axistype = "Dual Axis"
    else
        set axistype = "Single Axis"
    endif
endif

# Initialize for loops
#
@ naxis = 1
set axislet = ""
if ("$axistype" == "Dual Axis") then
    @ naxis = 2
    set axislet = "a"
endif
@ axnum = 1

# Get the setname from xcorr.com as fallback
#
if ("$setname" == "") then
    set setname = `sed -n "/InputFile[:space:]*\(.*\)$axislet.st/s//\1/p" $xcorrfile`
    if ($setname == "") then
        echo "ERROR: $pn - Cannot find DatasetName in edf file or $xcorrfile"
        exit 1
    endif
endif

# Set up the output file
#
if ("$outfile" == "") then
    set outfile = $rundir/$setname-snapshot.tar.gz
else
    if (-d "$outfile") set outfile = $outfile/$setname-snapshot.tar.gz
    set leadslash = `echo $outfile | sed -n '/^\//p'`
    if ("$leadslash" == "") set outfile = $rundir/$outfile
endif

# Get uname output if it can be written to current dir
#
if (-w .) then
    uname -a >! uname.out
    \ls -lrt >! lslrt.out
endif
set list = ($edffile)

# Loop on axis files, adding files if they exist
#
while ($axnum <= $naxis)
    foreach i ($oneAxisComLog)
        if (-e $i$axislet.com) set list = ($list $i$axislet.com)
        if (-e $i$axislet.log) set list = ($list $i$axislet.log)
    end
    foreach i ($oneAxisSetFiles)
        if (-e $setname$axislet$i) set list = ($list $setname$axislet$i)
    end
    @ axnum++
    set axislet = "b"
end

# Add combine files and other files
#
foreach i ($combineComLog)
    if (-e $i.com) set list = ($list $i.com)
    if (-e $i.log) set list = ($list $i.log)
end
foreach i ($singleFiles)
    if (-e $i) set list = ($list $i)
end

# Tar the file
#
tar -cvzf $outfile $list
if (-e uname.out) \rm -f uname.out
if (-e lslrt.out) \rm -f lslrt.out
if ($status) then
    echo "ERROR: $pn - making snapshot tar file"
    exit 1
endif
echo "Snapshot done, placed in $outfile"
exit 0

usage:
cat <<EOF
Usage: $pn [options] [Directory path to data set]
Packs all small files from single or dual axis data set into a gzipped tar file
By default, it will look in the current directory, unless a path to the 
directory with the data set is entered as the last argument.
Options:
    -o  filename   Set directory path or full filename of output file.  The
                     default output file is setname-snapshot.tar.gz in the
                     current directory.  If you enter a directory path without
                     a filename, a file with the default name will be written 
                     to that directory.  
    -e  filename   Name of .edf file to examine.  By default, the first .edf
                     file in a directory listing of *.edf will be used.
EOF
