#!/bin/csh -f
# Collects all the small files for diagnosing problems building tomograms
#
#  $Id$
#  Log at end
#

# Defaults
#
set pn = tomosnapshot
set curdir = .
set edffile = ""
set outfile = ""
set setname = ""
set axistype = ""
set comdir = tomosnapshot.cms.$$

# The file lists
#
set oneAxisSetFiles = (.rawtlt _peak.mod .prexf .prexg .seed .fid .3dmod .tlt \
fid.xyz .resid .resmod .tltxf _fid.xf local.xf .matmod .pl .ecd _orig.seed \
.xtilt .maggrad .zfac .defocus _erase.fid _3dfind.mod _flat.mod _flat.mod)
set oneAxisHeaders = (_orig.st .st .preali .ali .rec .mat _full.rec)
set singleFiles = (solvezero.xf solve.xf refine.xf warp.xf inverse.xf \
patch.out patch_vector.mod patch_vector_ccc.mod patch_region.mod lslrt.out \
uname.out processchunks.out processchunksa.out processchunksb.out \
processchunks.csh processchunksa.csh processchunksb.csh transferfid.coord \
combine.out volcombine.csh)

echo "$pn : enter $pn -h for usage"
while ($#argv > 0)
    switch ($argv[1])
    case -h:
        goto usage
    case -e:
        set edffile = $argv[2]
        shift; shift
        breaksw
    case -o:
        set outfile = $argv[2]
        shift; shift
        breaksw

    default:
        if ($#argv > 1) then
            echo "ERROR: $pn - unrecognized option $argv[1]"
            exit 1
        endif
        set curdir = $argv[1]
        shift
        if (! -d $curdir) then
            echo "ERROR: $pn - argument $curdir is not a directory"
            exit 1
        endif
        breaksw
    endsw
end

if ($?IMOD_DIR) then
    setenv PATH "$IMOD_DIR/bin:$PATH"
endif

# Get current directory before going to specified one
#
set rundir = `pwd`
cd "$curdir"

# Get edf file
#
if ("$edffile" == "") then
    set edfFiles = `ls *.edf`
    if ($status) then
        echo "WARNING: $pn - No edf file found in this directory"
    else
        set edffile = $edfFiles[1]
    endif
else if (! -e "$edffile") then
    echo "WARNING: $pn - $edffile does not exist"
    set edffile = ""
endif

# Get set name and axis type from edf file
#
if ("$edffile" != "")then
    set setname = `sed -n -e '/[[:cntrl:]]/s///g' -e '/Setup.DatasetName=/s///p' $edffile`
    set axistype = `sed -n -e '/[[:cntrl:]]/s///g' -e '/Setup.AxisType=/s///p' $edffile`
endif

# Get the axis type from xcorr.com as fallback
#
if ("$axistype" == "") then
    set xcorrfile = `\ls xcorr.com`
    if ($status) then
        set xcorrfile = `\ls xcorra.com`
        if ($status) then
            echo "ERROR: $pn - Cannot find AxisType in edf file or from command files"
            exit 1
        endif
        set axistype = "Dual Axis"
    else
        set axistype = "Single Axis"
    endif
endif

# Initialize for loops
#
@ naxis = 1
set axislet = ""
if ("$axistype" == "Dual Axis") then
    @ naxis = 2
    set axislet = "a"
endif
@ axnum = 1

# Get the setname from xcorr.com as fallback
#
if ("$setname" == "") then
    set setname = `sed -n -e '/[[:cntrl:]]/s///g -e "/InputFile[[:space:]]*\(.*\)$axislet.st/s//\1/p"' $xcorrfile`
    if ($setname == "") then
        echo "ERROR: $pn - Cannot find DatasetName in edf file or $xcorrfile"
        exit 1
    endif
endif

# Set up the output file
#
if ("$outfile" == "") then
    set outfile = "$rundir/$setname-snapshot"
else
    if (-d "$outfile") set outfile = "$outfile/$setname-snapshot"
    set leadslash = `echo $outfile | sed -n '/^\//p'`
    if ("$leadslash" == "") set outfile = "$rundir/$outfile"
endif

# Get uname output if it can be written to current dir
#
if (-w .) then
    uname -a >! uname.out
    imodinfo | head -n 1 >> uname.out
    printenv >> uname.out
    set cygthere = `uname | sed -n '/CYGWIN/p'`
    if ($cygthere != "") cygcheck -s -v -r >> uname.out
    \ls -lrt >! lslrt.out
    \ls -ld . >> lslrt.out
endif
set list = ($edffile)

# Get error logs from elsewhere: first scan etomo_err.log for files
#
set copylogs = ""
if (-e etomo_err.log) then
    set errlogs = `sed -n '/^Error log.*is in /s///p' etomo_err.log`
    if ("$errlogs" != "") then
        foreach i ($errlogs)
            if (-r "$i") then
                \cp -p -f $i .
                chmod u+rw $i:t
                set copylogs = ($copylogs $i:t)
            endif
        end
    endif
endif

# Then get last N from the log directory
#
if (! $?ETOMO_LOG_DIR) setenv ETOMO_LOG_DIR "$HOME/.etomologs"
if ("$ETOMO_LOG_DIR" == "") setenv ETOMO_LOG_DIR "$HOME/.etomologs"
set grabnum = 10
if (-r "$ETOMO_LOG_DIR" && -d "$ETOMO_LOG_DIR") then
    set errlogs = `cd "$ETOMO_LOG_DIR" ; ls -t etomo_err*.log | tail -n $grabnum`
    # Copy each one over.  Don't need to put it on list, they are in loglist
    if ("$errlogs" != "") then
        foreach i ($errlogs)
            if (-r "$ETOMO_LOG_DIR/$i") then
                \cp -p -f "$ETOMO_LOG_DIR/$i" .
                chmod u+rw $i
                set copylogs = ($copylogs $i)
            endif
        end
    endif
endif

# Get all com and log files and backups except the ta*.log
#
set comlist = `\find . -maxdepth 1 -name '*.com' -print`
set comlistb = `\find . -maxdepth 1 -name '*.com~' -print`
set loglist = `\find . -maxdepth 1 -name '*.log' -print | grep -v '\./ta'`
set loglistb = `\find . -maxdepth 1 -name '*.log~' -print | grep -v '\./ta'`
set list = ($list $comlistb $loglist $loglistb)

if (-w .) then
    \rm -rf $comdir
    mkdir -p $comdir
    foreach i ($comlist)
        \cp -p $i $comdir/$i:t:r
    end
    set list = ($list $comdir)
else
    set list = ($list $comlist)
endif

# Loop on axis files, adding files if they exist
#
while ($axnum <= $naxis)
    foreach i ($oneAxisSetFiles)
        if (-e $setname$axislet$i) set list = ($list $setname$axislet$i)
    end

    if (-w .) then
        foreach i ($oneAxisHeaders)
            if (-e $setname$axislet$i) header $setname$axislet$i >> uname.out
        end
    endif

    @ axnum++
    set axislet = "b"
end

# Add combine files and other files
#
foreach i ($singleFiles)
    if (-e $i) set list = ($list $i)
end

# Tar the file and clean up
#
tar -czvf "$outfile" $list
#gzip -f "$outfile"
if (-e uname.out) \rm -f uname.out
if (-e lslrt.out) \rm -f lslrt.out
if (-e $comdir) \rm -rf $comdir
if ("$copylogs" != "") \rm -f $copylogs
if ($status) then
    echo "ERROR: $pn - making snapshot tar file"
    exit 1
endif
echo "Snapshot done, placed in $outfile"
exit 0

usage:
cat <<EOF
Usage: $pn [options] [Directory path to data set]
Packs all small files from single or dual axis data set into a gzipped tar file
By default, it will look in the current directory, unless a path to the 
directory with the data set is entered as the last argument.
Options:
    -o  filename   Set directory path or full filename of output file.  The
                     default output file is setname-snapshot.tar.gz in the
                     current directory.  If you enter a directory path without
                     a filename, a file with the default name will be written 
                     to that directory.  
    -e  filename   Name of .edf file to examine.  By default, the first .edf
                     file in a directory listing of *.edf will be used.
EOF

#  $Log$
#  Revision 3.23  2010/02/24 23:59:40  mast
#  quote $HOME in case it has spaces
#
#  Revision 3.22  2009/03/24 23:47:33  mast
#  Put coms in a separate directory take extension off
#
#  Revision 3.21  2009/03/06 05:20:59  mast
#  Tar up directly with z and remove extensions to make it more mailable
#
#  Revision 3.20  2009/02/17 05:44:04  mast
#  Changing directory or copied file modes for Vista
#
#  Revision 3.19  2008/12/17 21:06:25  mast
#  Add maggrad and zfac
#
#  Revision 3.18  2008/12/05 22:26:05  mast
#  Collect error logs from ETOMO_LOG_DIR or .etomologs
#
#  Revision 3.17  2008/11/21 18:43:33  mast
#  Add combine.out
#
#  Revision 3.16  2007/03/02 19:36:52  mast
#  Added _orig.seed and .xtilt
#
#  Revision 3.15  2006/11/21 19:04:00  mast
#  Added patch_vector_ccc.mod
#
#  Revision 3.14  2006/06/29 15:25:55  mast
#  Fixed bug in getting setname introduced by last change
#
#  Revision 3.13  2006/06/06 23:04:49  mast
#  Moved control stripping before /p
#
#  Revision 3.12  2006/05/11 22:02:56  mast
#  Added transferfid.coord
#
#  Revision 3.11  2006/03/31 16:15:36  mast
#  Added processchunks files
#
#  Revision 3.10  2006/02/16 06:32:33  mast
#  Put IMOD on the path
#
#  Revision 3.9  2005/11/29 17:04:18  mast
#  Added processchunks.out
#
#  Revision 3.8  2005/10/05 05:47:21  mast
#  Captured all com and log files (including ~) except ta*.log
#
#  Revision 3.7  2005/06/30 20:25:53  mast
#  Fixed for SGI (no tar -z) and for spaces in path
#
#  Revision 3.6  2005/05/08 14:47:57  mast
#  Added files for montage
#
#  Revision 3.5  2005/02/04 17:30:32  mast
#  Fixed setname problems in Win & xcorr fallback, added headers & version
#
#  Revision 3.4  2004/10/22 23:39:57  mast
#  Added newst.com
#
#  Revision 3.3  2004/10/22 03:38:51  mast
#  Added environment output and cygcheck
#
#  Revision 3.2  2004/10/13 20:31:34  mast
#  Added ls -lrt output and ability to work without edf, fixed a few bugs.
#
#  Revision 3.1  2004/10/07 16:18:22  mast
#  Initial creation
#
