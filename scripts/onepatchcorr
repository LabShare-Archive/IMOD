#! /bin/csh -f
# onepatchcorr3d - to get local displacement between two volumes by 3d 
# cross-correlation at a single position
if ($#argv != 9) then
cat <<EOCAT
Usage: onepatchcorr  XSIZE YSIZE ZSIZE  XCEN YCEN ZCEN FILEA FILEB OUTPUT_FILE
Finds the displacement of the volume in FILEB relative to the volume in FILEA,
 in a local volume XSIZE by YSIZE by ZSIZE, centered at XCEN, YCEN, ZCEN, and
 places the displacement into OUTPUT_FILE
EOCAT
exit 1
endif

if ($?IMOD_DIR) then
    set path = ($IMOD_DIR/bin $path)
endif

set tmpdir = /usr/tmp
if ($?IMOD_TMPDIR) then
    if (-w $IMOD_TMPDIR) set tmpdir = $IMOD_TMPDIR
endif
if (! -w $tmpdir) set tmpdir = /tmp

@ nxl = $1; shift
@ nyl = $1; shift
@ nzl = $1; shift
@ xc = $1; shift
@ yc = $1; shift
@ zc = $1; shift
#
@ nxtap = ($nxl + 9) / 10
@ nytap = ($nyl + 9) / 10
@ nztap = ($nzl + 9) / 10
@ nxpad = ($nxl + 4) / 5
@ nypad = ($nyl + 4) / 5
@ nzpad = ($nzl + 4) / 5
#
set pad1 = $tmpdir/pad1.$$
set pad2 = $tmpdir/pad2.$$
set cortmp = $tmpdir/corr3d.$$
onintr clean
#
if (-e $argv[3]) \rm -f $argv[3]
tapervoledge >/dev/null <<HERESTRING
$argv[1]
$pad1
$nxl,$nyl,$nzl
$xc,$yc,$zc
$nxpad,$nypad,$nzpad
$nxtap,$nytap,$nztap
HERESTRING
tapervoledge >/dev/null <<HERESTRING
$argv[2]
$pad2
$nxl,$nyl,$nzl
$xc,$yc,$zc
$nxpad,$nypad,$nzpad
$nxtap,$nytap,$nztap
HERESTRING
set clipcom = "clip corr -3d -n 0 $pad1 $pad2 $cortmp"
set displ = `$clipcom | grep '^(' | sed '/.*(\(.*\)).*/s//\1/'`
echo $displ > $argv[3]

clean:
\rm -f $cortmp* $pad1* $pad2*
