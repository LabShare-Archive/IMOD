#!/bin/csh -f
# Script to copy .cmd wrappers to install directory
# First argument is install directory, second is directory of wrappers,
# then script names
#
#  $Id$
#  Log at end
#

set cshwrapper = tcshwrapper.cmd
set tcshwrapper = tcshwrapper.cmd
set bashwrapper = bashwrapper.cmd
set shwrapper = bashwrapper.cmd
set pywrapper = pywrapper.cmd

if ($#argv < 3) then
    echo "copywrappers: Too few arguments"
    exit 1
endif

set instdir = $argv[1]
set srcdir = $argv[2]
shift ; shift
set numarg = $#argv
while ($#argv > 0)
    if ((-x $argv[1] || $numarg == 1) && ($argv[1] != "settmpdir")) then
        set comsh = `head -n 1 $argv[1] | sed -e '/^.*bin\//s///' -e '/ .*$/s///' -e '/[[:cntrl:]]/s///g'`
        switch ("$comsh")
          case sh:
            set wrapper = $shwrapper
            breaksw
          case bash:
            set wrapper = $bashwrapper
            breaksw
          case csh:
            set wrapper = $cshwrapper
            breaksw
          case tcsh:
            set wrapper = $tcshwrapper
            breaksw
          case python:
            set wrapper = $pywrapper
            breaksw
          default:
            echo "copywrappers: No command shell found in first line of $argv[1]"
            exit 1
        endsw

        set name = $argv[1]:t
        sed -e "/%0/s//$name/" $srcdir/$wrapper >! $instdir/$name.cmd
        chmod a+rx $instdir/$name.cmd
    endif
    shift
end
exit 0

#
#  $Log$
#  Revision 1.5  2008/03/20 20:30:38  mast
#  Made it produced personalized wrapper with name, so command can be run
#  by full path
#
#  Revision 1.4  2007/04/10 19:41:18  mast
#  Make it detect command shell and copy the right wrapper; second arg is
#  now directory to find wrappers, not wrapper itself
#
#  Revision 1.3  2006/10/16 18:24:59  mast
#  Disable test for executable when only one argument
#
#  Revision 1.2  2006/10/16 18:06:10  mast
#  Added some debug output
#
#  Revision 1.1  2006/09/26 22:18:46  mast
#  Added to package
#
