#! /bin/csh -f
# reduceby2 - script to run reducemont in its simplest form to bin down by 2
#
#  $Author$
#
#  $Date$
#
#  $Revision$
#
#  $Log$
#  Revision 3.7  2006/02/16 06:47:09  mast
#  Stripped control chars from output of sed/header etc for Windows
#
#  Revision 3.6  2005/11/19 04:31:26  mast
#  Quote path setting to preserve spaces
#
#  Revision 3.5  2004/06/29 03:49:13  mast
#  Added nohup
#
#  Revision 3.4  2003/12/12 21:40:13  mast
#  Fix change in alterheader entry
#
#  Revision 3.3  2003/12/12 20:28:24  mast
#  Add binning option and option to preserve header pixel size
#

nohup
if ($#argv < 2) then
cat <<EOCAT 
Usage: reduceby2 [-p | -b n] Input_file Output_file
         runs Reducemont on the entire input file to reduce X and Y sizes by
         binning (2 by default)
       Options:
         -b n    Bin images by n - the default is to bin by 2
         -p      Preserve pixel size produced by Reducemont; otherwise the
                     pixel size will be set to 1
EOCAT
exit 1
endif
set preserve = 0
set binning = 2

while ($#argv > 2)
    switch ($argv[1])
    case -p:
      set preserve = 1
      shift
      breaksw
    case -b:
      set binning = $argv[2]
      shift ; shift
      breaksw
    default:
      echo "Illegal option: $argv[1]"
      exit 1
    endsw
end

set tmpdir = /usr/tmp
if ($?IMOD_DIR) then
    setenv PATH "$IMOD_DIR/bin:$PATH"
    if (-e "$IMOD_DIR/bin/settmpdir") source "$IMOD_DIR/bin/settmpdir"
endif

set tmppl = "$tmpdir/reducepl.$$"

reducemont <<EOCAT 
$argv[1]

$argv[2]
$tmppl
/
0
/
$binning
/
10000,10000,2,2
0
EOCAT
\rm -f "$tmppl"

if ($preserve == 1) exit 0

set nxyz =  `header -si $argv[2] | sed '/[[:cntrl:]]/s///g'`
alterheader <<EOF
$argv[2]
cel
$nxyz[1] $nxyz[2] $nxyz[3] /
done
EOF
