#!/bin/csh -f

# Script to take the adpStub and insert the 3dmod help pages and the man
# pages and library doc pages as subsections.
#
#  $Id$
#  Log at end
#

set instext1 = "INSERT 3DMOD.ADP"
set instext2 = "INSERT MAN PAGES"
set instext3 = "INSERT LIBRARY PAGES"
set adpout = IMOD.adp
set adpin = adpStub
set excludes = (cpuadoc pip imodenv uitest)
set instdir = .
if ($#argv > 0) then
    set instdir = $argv[1]/html
endif

awk "NR==1, /$instext1/" $adpin | grep -v "INSERT" >! $adpout
awk '/<DCF/,/<\/DCF/' 3dmodHelp/3dmod.adp | \
  sed -e '/DCF/d' -e '/\.\//s//\.\/3dmodHelp\//' >> $adpout
awk "/$instext1/, /$instext2/" $adpin | grep -v "INSERT" >> $adpout

foreach i ($instdir/man/*.html)
    set file = $i:r:t
    set include = 1
    foreach j ($excludes)
        if ("$file" == "$j") set include = 0
    end
    if ($include) cat <<EOF >> $adpout
    <section ref ="./man/$file.html" title="$file"/>
EOF
end

awk "/$instext2/, /$instext3/" $adpin | grep -v "INSERT" >> $adpout

foreach i (libhelp/*.html)
    set file = $i:r:t
    set title = `sed -n '/.*<title>\(.*\)<\/title>.*/s//\1/p' $i`
    if ("$title" == "") set title = $file
    
    cat <<EOF >> $adpout
    <section ref ="./libhelp/$file.html" title="$title"/>
EOF
end

awk "/$instext3/, /GobbledyGook/" $adpin | grep -v "INSERT" >> $adpout

#
#  $Log$
#  Revision 3.6  2008/03/26 22:21:15  mast
#  Fixed extraction of title from libray docs
#
#  Revision 3.5  2007/12/28 06:16:44  mast
#  Fixed source for libhelp files
#
#  Revision 3.4  2007/12/18 15:48:40  mast
#  fixed foreach - Library docs are still in libhelp here when this is run
#
#  Revision 3.3  2007/12/17 06:52:24  mast
#  Excluded non-programs from man page, and added library docs
#
#  Revision 3.2  2006/06/19 14:11:27  mast
#  Fixed to work with install dir, fixed path to 3dmodHelp
#
#  Revision 3.1  2006/06/18 23:44:24  mast
#  Added to package
