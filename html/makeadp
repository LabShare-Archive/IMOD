#!/bin/csh -f

# Script to take the adpStub and insert the 3dmod help pages and the man
# pages and library doc pages as subsections.
#
#  $Id$
#
#  $Log$
#  Revision 3.2  2006/06/19 14:11:27  mast
#  Fixed to work with install dir, fixed path to 3dmodHelp
#
#  Revision 3.1  2006/06/18 23:44:24  mast
#  Added to package
#

set instext1 = "INSERT 3DMOD.ADP"
set instext2 = "INSERT MAN PAGES"
set instext3 = "INSERT LIBRARY PAGES"
set adpout = IMOD.adp
set adpin = adpStub
set excludes = (cpuadoc pip imodenv uitestadoc uitestaxisadoc)
set instdir = .
if ($#argv > 0) then
    set instdir = $argv[1]/html
endif

awk "NR==1, /$instext1/" $adpin | grep -v "INSERT" >! $adpout
awk '/<DCF/,/<\/DCF/' 3dmodHelp/3dmod.adp | \
  sed -e '/DCF/d' -e '/\.\//s//\.\/3dmodHelp\//' >> $adpout
awk "/$instext1/, /$instext2/" $adpin | grep -v "INSERT" >> $adpout

foreach i ($instdir/man/*.html)
    set file = $i:r:t
    set include = 1
    foreach j ($excludes)
        if ("$file" == "$j") set include = 0
    end
    if ($include) cat <<EOF >> $adpout
    <section ref ="./man/$file.html" title="$file"/>
EOF
end

awk "/$instext2/, /$instext3/" $adpin | grep -v "INSERT" >> $adpout

foreach i ($instdir/libhelp/*.html)
    set file = $i:r:t
    set title = `grep -i -A 1 '<title>' $i | tail -n 1`
    if ("$title" == "") set title = $file
    
    cat <<EOF >> $adpout
    <section ref ="./libdoc/$file.html" title="$title"/>
EOF
end

awk "/$instext3/, /GobbledyGook/" $adpin | grep -v "INSERT" >> $adpout
