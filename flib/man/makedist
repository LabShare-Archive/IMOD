#!/bin/csh -f
#
# Script to convert .pre1 files to .1 files, and .1 files to .html files,
# with a separate call to clean up the .1 files generated from .pre1
#
#  $Author$
#
#  $Date$
#
#  $Revision$
#
#  $Log$
#  Revision 3.1  2003/10/08 17:17:43  mast
#  Changes to work with autodoc files
#

set instext = "INSERT OPTION"

set prelist = `\find . -type f -name '*.pre1'`

if ($argv[1] == "clean") goto clean

@ ind = 1
while ($ind <= $#prelist)
    set i = $prelist[$ind]
    set manout = ${i:r}.1
    echo "converting $i to $manout"
    awk "NR==1, /$instext/" $i | grep -v "$instext" >! $manout
    $argv[1]/autodoc2man -1 2 ${i:r} >> $manout
    if ($status) then
        echo "Error getting manpage text from option file for ${i:r}"
        exit 1
    endif
    awk "/$instext/, /GobbledyGook/" $i | grep -v "$instext" >> $manout
    @ ind++
end    

foreach i (*.1)
    echo "converting $i to ${i:r}.html"
    $argv[1]/mantohtml < $i $i:t:r >! ${i:r}.html
end

exit 0

clean:
@ ind = 1
while ($ind <= $#prelist)
    \rm -f ${prelist[$ind]:r}.1
    @ ind++
end
